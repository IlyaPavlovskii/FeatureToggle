plugins {
    alias(libs.plugins.com.android.library)
    alias(libs.plugins.org.jetbrains.kotlin.android)
    alias(libs.plugins.org.jetbrains.kotlin.serialization)
    id 'maven-publish'
    id 'signing'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'io.github.ilyapavlovskii'
            artifactId = 'debug.panel'
            version = libs.versions.versionName.get()
            artifact("$buildDir/outputs/aar/debug-panel-debug.aar")

            pom {
                name = "FeatureToggle"
                description = 'Android FeatureToggle debug panel to edit toggles by separate launcher.'
                url = 'https://github.com/IlyaPavlovskii/FeatureToggle/'
                licenses {
                    license {
                        name = 'Apache License, Version 2.0'
                        url = 'https://github.com/IlyaPavlovskii/FeatureToggle/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'IlyaPavlovskii'
                        name = 'Ilia Pavlovskii'
                        email = 'pavlovskii.ilya@gmail.com'
                    }
                }
                scm {
                    url = 'https://github.com/IlyaPavlovskii/FeatureToggle/'
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}

android {
    namespace 'io.github.ilyapavlovskii.feature.toggle.debug.panel'
    compileSdk libs.versions.compileSdk.get().toInteger()

    defaultConfig {
        minSdk libs.versions.minSdk.get().toInteger()
        targetSdk libs.versions.targetSdk.get().toInteger()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        compose true
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.4'
    }
}

dependencies {
    implementation libs.androidx.core.core.ktx
    implementation libs.androidx.activity.activity.compose
    implementation platform(libs.androidx.compose.compose.bom)
    implementation libs.androidx.compose.ui
    implementation libs.androidx.compose.ui.ui.graphics
    implementation libs.androidx.compose.ui.ui.tooling.preview
    implementation libs.androidx.compose.material3
    implementation libs.org.jetbrains.kotlinx.kotlinx.serialization.json
    implementation project(":feature-toggle")
    implementation project(":utils:design-system")
}